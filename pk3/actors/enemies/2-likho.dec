ACTOR Likho 31091
{
//$Category Nocturne-Monsters
    Health 60
    Radius 20
    Height 56
    Speed 10
    PainChance 180
    PainChance "Spear", 255
    DamageFactor "Bullet", 2.0
    Scale 0.45
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "likho/see"
    PainSound "likho/pain"
    DeathSound "likho/death"
    ActiveSound "likho/idle"
    Obituary " "
    States
    {
    Spawn:
        LIKH A 10 A_Look
        Loop
    See:
        LIKH AABBCCDD 3 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_Jump(64,"Missile2")
        LIKH E 6 A_FaceTarget
        LIKH E 5 bright A_CustomMissile("LikhoBall",24)
        Goto See
    Missile2:
        LIKH E 5 A_FaceTarget
        LIKH E 4 bright A_CustomMissile("LikhoBall",24)
        LIKH E 5 A_FaceTarget
        LIKH E 4 bright A_CustomMissile("LikhoBall",24)
        Goto See
    Pain:
        LIKH A 2
        LIKH A 2 A_Pain
        Goto See
    Death:
        TNT1 A 0 A_SetScale(1.0,1.0)
        BAUK D 4 A_Scream
        BAUK EF 4
        BAUK G 4 A_NoBlocking
        BAUK HIJKLM 4
        BAUK N -1
        Stop
    }
}

ACTOR LikhoBall
{
    Radius 6
    Height 8
    Speed 15
    Damage (random(10,20))
    Projectile
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    RenderStyle Add
    Alpha 0.99
    Scale 0.75
    SeeSound "likho/attack"
    DeathSound "likho/hit"
    States
    {
    Spawn:
        LBAL AAAABBBB 1 Bright A_SpawnItemEx("LikhoBallTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        LBAL CDEFG 3 Bright
        Stop
    }
}

ACTOR LikhoBallTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    RenderStyle Add
    Alpha 0.99
    Scale 0.65
    States
    {
    Spawn:
        TNT1 A 0
        LBAL AB 1 Bright A_FadeOut(0.1)
        loop
    }
}
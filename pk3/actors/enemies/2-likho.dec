ACTOR Likho 31091
{
//$Category Nocturne-Monsters
    Health 80
    Radius 20
    Height 56
    Speed 10
    PainChance 180
    PainChance "Spear", 255
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    Obituary " "
    States
    {
    Spawn:
        TROO AB 10 A_Look
        Loop
    See:
        TROO AABBCCDD 3 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_Jump(64,"Missile2")
        TROO EF 6 A_FaceTarget
        TROO G 5 A_CustomMissile("LikhoBall")
        Goto See
    Missile2:
        TROO EF 5 A_FaceTarget
        TROO G 4 A_CustomMissile("LikhoBall")
        TROO EF 5 A_FaceTarget
        TROO G 4 A_CustomMissile("LikhoBall")
        Goto See
    Pain:
        TROO H 2
        TROO H 2 A_Pain
        Goto See
    Death:
        TROO I 8
        TROO J 8 A_Scream
        TROO K 6
        TROO L 6 A_NoBlocking
        TROO M -1
        Stop
    XDeath:
        TROO N 5
        TROO O 5 A_XScream
        TROO P 5
        TROO Q 5 A_NoBlocking
        TROO RST 5
        TROO U -1
        Stop
    }
}

ACTOR LikhoBall
{
    Radius 6
    Height 8
    Speed 15
    Damage (random(10,20))
    Projectile
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    RenderStyle Add
    Alpha 0.99
    Scale 0.75
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        BAL1 AAAABBBB 1 Bright A_SpawnItemEx("LikhoBallTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        BAL1 CDE 6 Bright
        Stop
    }
}

ACTOR LikhoBallTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    RenderStyle Add
    Alpha 0.99
    Scale 0.65
    States
    {
    Spawn:
        TNT1 A 0
        BAL1 A 1 Bright A_FadeOut(0.1)
        loop
    }
}
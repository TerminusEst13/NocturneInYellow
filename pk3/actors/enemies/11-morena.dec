// 31100 - Morena, first time

actor Morena1 31100
{
//$Category Nocturne-Bosses
    Health 2200
    Radius 24
    Height 76
    Speed 10
    PainChance 64
    Mass 0x7FFFFFFF
    Monster
    +FLOORCLIP
    +LOWGRAVITY
	+FLOATBOB
    +MISSILEMORE
    +DORMANT
    SeeSound "morena/sight"
    PainSound "morena/pain"
    DeathSound "morena/death"
    Obituary " "
    States
    {
    Spawn:
        MREN A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN A 1 A_Chase("","")
        MREN AAA 1 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_JumpIfCloser(1536,"Missile0")
        TNT1 A 0 A_FaceTarget
        MREN A 1
        TNT1 A 0 A_Recoil(-50)
        goto See
    Missile0:
        TNT1 A 0 A_Jump(16,"Teleport")
        TNT1 A 0 A_Jump(32,"Spin")
        TNT1 A 0 A_Jump(64,"SideSwipe")
        TNT1 A 0 A_Jump(64,"Missile2")
    Missile1:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("MorenaShot",32,0,frandom(-3,3),CMF_OFFSETPITCH,frandom(-2,2))
        Goto See
    Missile2:
        TNT1 A 0 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        TNT1 A 0 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        TNT1 A 0 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN DDDDDD 1 A_Chase("","")
        Goto See
    SideSwipe:
        TNT1 A 0 A_PlaySoundEx("morena/attack","soundslot5",0,0)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 ThrustThingZ(0,16,0,0)
        MREN BC 2
        TNT1 A 0 A_Jump(255,"SideSwipe1","SideSwipe2")
    SideSwipe1:
        TNT1 A 0 ThrustThing(angle*256/360+64, 32, 0, 0)
        goto SideSwipeContinue
    SideSwipe2:
        TNT1 A 0 ThrustThing(angle*256/360+192, 32, 0, 0)
    SideSwipeContinue:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("MorenaShot",32,0,frandom(-1,1),CMF_OFFSETPITCH,frandom(-0.25,0.25))
        MREN D 3
        TNT1 A 0 A_CheckFloor("SideSwipeFinish")
        TNT1 A 0 A_GiveInventory("Counter",random(1,2))
        TNT1 A 0 A_JumpIfInventory("Counter",70,"SideSwipeFinish")
        loop
    SideSwipeFinish:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN AAA 1 A_Chase("","")
        goto See
    Spin:
        TNT1 A 0 A_PlaySoundEx("morena/attack","soundslot5",0,0)
        MREN BC 2
    Spin2:
        TNT1 A 0 A_CustomMissile("MorenaLightShot",32,0,frandom(-1,1),CMF_AIMDIRECTION,frandom(-0.25,0.25))
        MREN D 1 A_SetAngle(angle+10)
        TNT1 A 0 A_GiveInventory("Counter",1)
        TNT1 A 0 A_JumpIfInventory("Counter",35,"SpinFinish")
        loop
    SpinFinish:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN A 1
        goto See
    Teleport:
        TNT1 A 0 A_SpawnItemEx("FakeMorena",0,0,0,0,0,0,0,32)
        TNT1 AAAAAA 0 A_CustomMissile("MorenaHomingShot",32,0,frandom(-180,180),CMF_OFFSETPITCH,frandom(-5,5))
        MREN A 1
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
        TNT1 A 35
        goto See
    Pain:
        TNT1 A 0 A_TakeInventory("Counter",100)
        TNT1 A 0 A_Jump(64,"Teleport")
        MREN A 2
        MREN A 2 A_Pain
        Goto See
    Death:
        MREN A 1
        MREN AAAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("DeathExplosionSmall",frandom(-16,16),frandom(-16,16),frandom(16,42),0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("DeathExplosionLarge",0,0,32,0,0,0,0,32)
        MREN AAAAAAAAAA 1 A_FadeOut(0.1)
        MREN A 1
        Stop
    }
}

ACTOR MorenaShot
{
    Radius 6
    Height 8
    Speed 40
    Damage (random(5,10))
    Projectile
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    RenderStyle Add
    Alpha 0.99
    Scale 0.75
    SeeSound "morena/knifeshot"
    DeathSound "morena/knifehit"
    States
    {
    Spawn:
        DGGR A 2 Bright
        Loop
    Death:
        LBAL CDEFG 3 Bright
        Stop
    }
}

actor FakeMorena
{
    Radius 20
    Height 56
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Translucent
    Alpha 1.00
    States
    {
    Spawn:
        MREN A 15
        MREN AAAAAAAAAAAA 3 A_FadeOut(0.1)
        stop
    }
}

actor MorenaLightShot : MorenaShot
{
    Scale 0.45
    Damage (random(1,5))
}

actor MorenaHomingShot
{
    Radius 6
    Height 8
    Speed 10
    Damage (random(10,20))
    Projectile
    +RANDOMIZE
    +SEEKERMISSILE
    +THRUSPECIES
    Species "Monster"
    ReactionTime 50
    RenderStyle Add
    Alpha 1
    SeeSound "morena/homingshot"
    DeathSound "morena/hominghit"
    States
    {
    Spawn:
        MSOL AA 2 Bright A_SeekerMissile(10,20, SMF_PRECISE)
        TNT1 A 0 A_CountDown
        Loop
    Death:
        MSOL BCDEFGH 4 Bright
        Stop
    }
}




// 31101 - Morena, second time


actor Morena2 31101
{
//$Category Nocturne-Bosses
    Health 3750
    Radius 24
    Height 76
    Speed 14
    PainChance 64
    Mass 0x7FFFFFFF
    Monster
    +FLOORCLIP
	+FLOATBOB
    +LOWGRAVITY
    +MISSILEMORE
    +DORMANT
    MinMissileChance 10
    RadiusDamageFactor 0.5
    SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary " "
    States
    {
    Spawn:
        MREN A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN A 1 A_Chase("","")
        MREN AAA 1 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_JumpIfCloser(1536,"Missile0")
        TNT1 A 0 A_FaceTarget
        MREN A 1
        TNT1 A 0 A_Recoil(-50)
        goto See
    Missile0:
        TNT1 A 0 A_Jump(32,"Teleport")
        TNT1 A 0 A_Jump(64,"Spin")
        TNT1 A 0 A_Jump(128,"SideSwipe")
        TNT1 A 0 A_Jump(128,"Missile2")
    Missile1:
        TNT1 A 0 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-3,3),CMF_OFFSETPITCH,frandom(-2,2))
        Goto See
    Missile2:
        TNT1 A 0 A_FaceTarget
        TNT1 AAAAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        MREN B 1 A_Chase("","")
        MREN B 0 A_Chase("","")
        TNT1 A 0 A_FaceTarget
        TNT1 AAAAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        MREN C 1 A_Chase("","")
        MREN C 0 A_Chase("","")
        TNT1 A 0 A_FaceTarget
        TNT1 AAAAA 0 A_CustomMissile("MorenaShot",32,0,frandom(-5,5),CMF_OFFSETPITCH,frandom(-3,3))
        MREN DDDDDD 1 A_Chase("","")
        Goto See
    SideSwipe:
        TNT1 A 0 A_PlaySoundEx("morena/attack","soundslot5",0,0)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 ThrustThingZ(0,16,0,0)
        MREN BC 2
        TNT1 A 0 A_Jump(255,"SideSwipe1","SideSwipe2")
    SideSwipe1:
        TNT1 A 0 ThrustThing(angle*256/360+64, 32, 0, 0)
        goto SideSwipeContinue
    SideSwipe2:
        TNT1 A 0 ThrustThing(angle*256/360+192, 32, 0, 0)
    SideSwipeContinue:
        TNT1 A 0 A_FaceTarget
        TNT1 AA 0 A_CustomMissile("MorenaShot",32,0,frandom(-1,1),CMF_OFFSETPITCH,frandom(-0.25,0.25))
        MREN D 2
        TNT1 A 0 A_CheckFloor("SideSwipeFinish")
        TNT1 A 0 A_GiveInventory("Counter",random(2,5))
        TNT1 A 0 A_JumpIfInventory("Counter",70,"SideSwipeFinish")
        loop
    SideSwipeFinish:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN AAA 1 A_Chase("","")
        goto See
    Spin:
        TNT1 A 0 A_PlaySoundEx("morena/attack","soundslot5",0,0)
        MREN BC 2
    Spin2:
        TNT1 AA 0 A_CustomMissile("MorenaLightShot",32,0,frandom(-5,5),CMF_AIMDIRECTION,frandom(-2.5,2.5))
        MREN D 1 A_SetAngle(angle+10)
        TNT1 A 0 A_GiveInventory("Counter",1)
        TNT1 A 0 A_JumpIfInventory("Counter",35,"SpinFinish")
        loop
    SpinFinish:
        TNT1 A 0 A_TakeInventory("Counter",100)
        MREN A 1
        goto See
    Teleport:
        TNT1 A 0 A_SpawnItemEx("FakeMorena",0,0,0,0,0,0,0,32)
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MorenaHomingShot",32,0,frandom(-180,180),CMF_OFFSETPITCH,frandom(-5,5))
        MREN A 1
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
        TNT1 A 35
        TNT1 A 0 A_Jump(16,"Teleport")
        TNT1 A 0 A_Jump(32,"Spin")
        TNT1 A 0 A_Jump(64,"SideSwipe")
        TNT1 A 0 A_Jump(64,"Missile2")
        goto See
    Pain:
        TNT1 A 0 A_TakeInventory("Counter",100)
        TNT1 A 0 A_Jump(128,"Teleport")
        MREN A 2
        MREN A 2 A_Pain
        Goto See
    Death:
        MREN A 1
        TNT1 A 0 A_JumpIfInTargetInventory("NoStory",1,"Deathsplosion")
        MREN AAAAAAAAAA 1 A_FadeOut(0.1)
        MREN A 1
        Stop
    DeathSplosion:
        MREN AAAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("DeathExplosionSmall",frandom(-16,16),frandom(-16,16),frandom(16,42),0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("DeathExplosionLarge",0,0,32,0,0,0,0,32)
        MREN AAAAAAAAAA 1 A_FadeOut(0.1)
        MREN A 1
        Stop
    }
}
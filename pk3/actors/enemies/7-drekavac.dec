ACTOR Drekavac 31096
{
//$Category Nocturne-Monsters
    Health 250
    Radius 20
    Height 56
    Speed 16
    PainChance 64
    PainChance "Spear", 255
    MeleeThreshold 256
    MeleeRange 96
    MinMissileChance 64
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    DamageFactor "Spear", 2.0
    SeeSound "drekavac/see"
    PainSound "drekavac/pain"
    DeathSound "drekavac/death"
    ActiveSound "drekavac/screamfar"
    States
    {
    Spawn:
        INCA A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_Jump(64,"See2")
        INCA AABBCCDDEEFF 1 A_Chase
        Loop
    See2:
        INCA AABBCCDDEEFF 1 A_FastChase
        goto See
    Melee:
        INCA A 4
        TNT1 A 0 Radius_Quake(3,7,0,16,0)
        TNT1 A 0 A_GiveToTarget("DrekavacScreamDamage",1)
        TNT1 A 0 A_Blast(BF_DONTWARN,255,256,30,"BlankPuff","")//A_RadiusThrust(512,256,RTF_NOTMISSILE,128)
        INCA L 4 A_PlaySoundEx("drekavac/scream","soundslot6",0,0)
        TNT1 A 0 Radius_Quake(3,7,0,16,0)
        INCA L 4 A_Blast(BF_DONTWARN,255,256,30,"BlankPuff","")//A_RadiusThrust(512,256,RTF_NOTMISSILE,128)
        TNT1 A 0 Radius_Quake(3,7,0,16,0)
        INCA L 4 A_Blast(BF_DONTWARN,255,256,30,"BlankPuff","")//A_RadiusThrust(512,256,RTF_NOTMISSILE,128)
        TNT1 A 0 Radius_Quake(3,7,0,16,0)
        INCA L 4 A_Blast(BF_DONTWARN,255,256,30,"BlankPuff","")//A_RadiusThrust(512,256,RTF_NOTMISSILE,128)
        Goto See
    Missile:
               // Played on ACS instead of Decorate because, for some reason, even with
               // $limit 0 set on the sound, the sounds still cancel each other out.
        TNT1 A 0 ACS_ExecuteWithResult(433,12)//A_ActiveSound//A_PlaySoundEx("drekavac/screamfar","soundslot5",0,0)
        TNT1 A 0 Radius_Quake(1,5,0,16,0)
        TNT1 A 0 A_RadiusThrust(1024,1024,RTF_NOTMISSILE,896)
        Goto See
    Pain:
        INCA L 4
        INCA L 4 A_Pain
        Goto See
    Death:
        INCA LM 7
        INCA N 7 A_Scream
        INCA O 7 A_NoBlocking
        INCA P 7
        INCA Q -1
        Stop
    }
}

ACTOR DrekavacScreamDamage : CustomInventory
{
    Inventory.MaxAmount 0
    Inventory.Amount 0
    -INVBAR
    +ALWAYSPICKUP
    +AUTOACTIVATE
    States
    {
    Spawn:
        TNT1 A 1
        stop
    Pickup:
    Use:
        TNT1 A 0
        TNT1 A 0 A_SetAngle(angle+frandom(-45,45))
        TNT1 A 0 A_SetPitch(pitch+frandom(-15,15))
        TNT1 A 0 DamageThing(random(1,3))
        stop
    }
}
ACTOR Nocnitsa 31093
{
//$Category Nocturne-Monsters
    Health 200
    Radius 32
    Height 56
    Speed 13
    PainChance 128
    PainChance "Spear", 255
    DamageFactor "Bullet", 1.5
    RenderStyle Add
    Alpha 0.75
    Damage (random(5,10))
    Monster
    +FLOAT
    +NOGRAVITY
    +MISSILEMORE
    +DONTFALL
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "nocnitsa/see"
    PainSound "nocnitsa/pain"
    DeathSound "nocnitsa/death"
    ActiveSound "nocnitsa/idle"
    Obituary " "
    States
    {
    Spawn:
        NOCN A 10 Bright A_Look
        Loop
    See:
        NOCN A 3 Bright A_Chase
        Loop
    Missile:
        TNT1 A 0 A_PlaySoundEx("nocnitsa/attack","soundslot5",0,0)
        NOCN B 3 Bright A_FaceTarget
        NOCN C 3 Bright A_SkullAttack(35)
        NOCN CCCCCCCCCCCCCCCCC 3 Bright
        TNT1 A 0 A_Stop
        NOCN B 4 Bright
        Goto See
    Melee:
        NOCN B 5 Bright
        NOCN C 5 Bright A_CustomMeleeAttack(random(10,15),"","nezhit/hit")
        goto See
    Pain:
        NOCN D 3 Bright
        NOCN D 3 Bright A_Pain
        Goto See
    Death:
        NOCN D 2 Bright A_Scream
        NOCN DD 2 Bright A_FadeOut(0.1)
        TNT1 A 0 A_NoBlocking
        NOCN DDDDDDDDDDDDDDD 1 Bright A_FadeOut(0.05)
        Stop
    }
}
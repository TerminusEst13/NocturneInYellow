ACTOR Ala 31095
{
//$Category Nocturne-Monsters
    Health 250
    Radius 31
    Height 56
    Speed 12
    PainChance 128
    PainChance "Spear", 255
    Scale 0.75
    Monster
    +FLOAT
    +NOGRAVITY
    +MISSILEMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "ala/see"
    PainSound "ala/pain"
    DeathSound "ala/death"
    ActiveSound "ala/idle"
    Obituary " "
    States
    {
    Spawn:
        WICK A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_Jump(64,"See1")
        TNT1 A 0 A_Jump(64,"See2")
        WICK AABBCCDD 3 A_Chase
        Loop
    See1:
        WICK EEFFGGHH 2 A_Chase
        Loop
    See2:
        WICK EEFFGGHH 2 A_FastChase
        Loop
    Missile:
        //TNT1 A 0 A_JumpIfCloser(256,"BlowBack")
        TNT1 A 0 A_PlaySoundEx("ala/attack","soundslot5",0,0)
        WICK IJ 3 A_FaceTarget
        WICK K 3 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("AlaHail",32,0,frandom(-10,10),CMF_OFFSETPITCH,frandom(-4,4))
        WICK L 4 Bright A_CustomMissile("AlaHail",32,0,0,0)
        Goto See
    Pain:
        WICK M 3
        WICK M 3 A_Pain
        Goto See
    Death:
        WICK N 5 A_Scream
        WICK OP 5
        WICK Q 5
        WICK R 5
        Wait
    Crash:
        WICK S 1 A_SetFloorClip
        WICK S 4
        WICK TUV 5
        WICK W -1
        Stop
    }
}

ACTOR AlaHail
{
    Radius 6
    Height 8
    Speed 15
    Damage (random(10,15))
    Scale 0.075
    RenderStyle Add
    Alpha 0.99
    Projectile
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    DeathSound "ala/impact"
    States
    {
    Spawn:
        ABLL A 1 Bright
        Loop
    Death:
        TNT1 A 0
        TNT1 AAAAAA 0 A_SpawnItemEx("AlaHailSpatter",0,0,0,frandom(-1.25,1.25),frandom(-1.25,1.25),frandom(-1,1),random(-180,180),32)
        TNT1 A 1
        Stop
    }
}

ACTOR AlaHailSpatter
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Alpha 0.99
    Scale 0.2
    States
    {
    Spawn:
        ABLL BCDEFGHIJKLM 1 Bright
        stop
    }
}
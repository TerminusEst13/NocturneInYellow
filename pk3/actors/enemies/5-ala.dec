ACTOR Ala 31095 // Absolutely no relation to deities.
{
//$Category Nocturne-Monsters
    Health 250
    Radius 31
    Height 56
    Speed 12
    PainChance 128
    PainChance "Spear", 255
    DamageFactor "Bullet", 1.5
    Scale 0.75
    Monster
    +FLOAT
    +NOGRAVITY
    +MISSILEMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "ala/see"
    PainSound "ala/pain"
    DeathSound "ala/death"
    ActiveSound "ala/idle"
    Obituary " "
    States
    {
    Spawn:
        ALAH A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_Jump(64,"See1")
        TNT1 A 0 A_Jump(64,"See2")
        ALAH AA 3 A_Chase
        Loop
    See1:
        ALAH AA 2 A_Chase
        TNT1 A 0 A_Jump(1,"See")
        Loop
    See2:
        ALAH AA 2 A_FastChase
        TNT1 A 0 A_Jump(1,"See")
        Loop
    Missile:
        //TNT1 A 0 A_JumpIfCloser(256,"BlowBack")
        TNT1 A 0 A_PlaySoundEx("ala/attack","soundslot5",0,0)
        ALAH BB 2 A_FaceTarget
        ALAH C 4 A_FaceTarget
        TNT1 AAA 0 A_CustomMissile("AlaHail",32,0,frandom(-10,10),CMF_OFFSETPITCH,frandom(-4,4))
        ALAH D 4 Bright A_CustomMissile("AlaHail",32,0,0,0)
        Goto See
    Pain:
        ALAH B 3
        ALAH B 3 A_Pain
        Goto See
    Death:
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("AlaDeathHail",frandom(-2,2),frandom(-2,2),frandom(16,32),frandom(-5,5),frandom(-5,5),frandom(-5,5),0,160)
        ALAH E 5 A_Scream
        ALAH E -1
        Stop
    }
}

ACTOR AlaDeathHail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Alpha 0.99
    Scale 0.5
    States
    {
    Spawn:
        ABLL BCDEFGHIJKLM 2 Bright
        stop
    }
}

ACTOR AlaHail
{
    Radius 6
    Height 8
    Speed 15
    Damage (random(10,15))
    Scale 0.075
    RenderStyle Add
    Alpha 0.99
    Projectile
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    DeathSound "ala/impact"
    States
    {
    Spawn:
        ABLL A 1 Bright
        Loop
    Death:
        TNT1 A 0
        TNT1 AAAAAA 0 A_SpawnItemEx("AlaHailSpatter",0,0,0,frandom(-1.25,1.25),frandom(-1.25,1.25),frandom(-1,1),random(-180,180),32)
        TNT1 A 1
        Stop
    }
}

ACTOR AlaHailSpatter
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Alpha 0.99
    Scale 0.2
    States
    {
    Spawn:
        ABLL BCDEFGHIJKLM 1 Bright
        stop
    }
}
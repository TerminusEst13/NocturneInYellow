ACTOR Bauk 31092
{
//$Category Nocturne-Monsters
    Health 125
    Radius 30
    Height 56
    Speed 12
    PainChance 160
    PainChance "Spear", 255
    MaxTargetRange 1024
    DamageFactor "Bullet", 2.0
    Monster
    RenderStyle Add
    Alpha 0.75
    +FLOORCLIP
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    +MISSILEMORE
    MeleeRange 96
    Species "Monster"
    SeeSound "bauk/see"
    AttackSound "bauk/attack"
    PainSound "bauk/pain"
    DeathSound "bauk/death"
    ActiveSound "bauk/idle"
    Obituary " "
    States
    {
    Spawn:
        BAUK A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_TakeInventory("Counter",0x7FFFFFFF)
        TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
        TNT1 A 0 A_JumpIfCloser(224,"See3")
        TNT1 A 0 A_JumpIfCloser(1024,"See2")
        BAUK AABB 2 A_Chase
        Loop
    See2:
        BAUK AABB 2 A_FastChase
        goto See
    See3:
        TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
        BAUK AABB 2 A_Chase
        BAUK AA 2 A_FastChase
        BAUK BB 2 A_Chase
        goto See
    Missile:
        TNT1 A 0 A_Stop
        BAUK C 1 A_FaceTarget
        TNT1 A 0 ThrustThingZ(0,21,0,1)
        TNT1 A 0 A_Recoil(-35)
        TNT1 A 0 A_PlaySound("bauk/attack",CHAN_AUTO,1.0,0,ATTN_NORM)
        BAUK CC 2 A_FaceTarget
    MissileFlight:
        BAUK C 1 A_JumpIfCloser(64,"MissileHit")
        TNT1 A 0 A_JumpIfInventory("Counter",70,"MissileFinish")
        TNT1 A 0 A_GiveInventory("Counter",1)
        TNT1 A 0 A_CheckFloor("MissileFinish")
        loop
    MissileHit:
        TNT1 A 0 A_CustomMeleeAttack(random(10,15),"bauk/attack","")
    MissileFinish:
        TNT1 A 0 A_TakeInventory("Counter",0x7FFFFFFF)
        BAUK A 5
        Goto See
    Melee:
        BAUK C 4 A_FaceTarget
        BAUK C 5 A_CustomMeleeAttack(random(10,15),"bauk/attack","bauk/attack")
        Goto See
    Pain:
        BAUK A 2
        BAUK A 2 A_Pain
        Goto See
    Death:
        BAUK D 4 A_Scream
        BAUK EF 4
        BAUK G 4 A_NoBlocking
        BAUK HIJKLM 4
        BAUK N -1
        Stop
  }
}
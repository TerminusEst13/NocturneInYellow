ACTOR Bauk 31092
{
//$Category Nocturne-Monsters
    Health 125
    Radius 30
    Height 56
    Speed 12
    PainChance 160
    PainChance "Spear", 255
    MaxTargetRange 1024
    Monster
    +FLOORCLIP
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "bauk/see"
    AttackSound "bauk/attack"
    PainSound "bauk/pain"
    DeathSound "bauk/death"
    ActiveSound "bauk/idle"
    Obituary " "
    States
    {
    Spawn:
        SARG A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
        TNT1 A 0 A_JumpIfCloser(224,"See3")
        TNT1 A 0 A_JumpIfCloser(1024,"See2")
        SARG AABBCCDD 2 A_Chase
        Loop
    See2:
        SARG AABBCCDD 2 A_FastChase
        goto See
    See3:
        TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
        SARG AA 2 A_FastChase
        SARG BB 2 A_Chase
        SARG CC 2 A_FastChase
        SARG DD 2 A_Chase
        goto See
    Missile:
        TNT1 A 0 A_Stop
        SARG A 1 A_FaceTarget
        TNT1 A 0 ThrustThingZ(0,21,0,1)
        TNT1 A 0 A_Recoil(-35)
        TNT1 A 0 A_PlaySound("bauk/attack",CHAN_AUTO,1.0,0,ATTN_NORM)
        SARG EF 5 A_FaceTarget
        SARG G 5 A_CustomMeleeAttack(random(15,20),"bauk/attack","bauk/attack")
        SARG A 5
        Goto See
    Melee:
        SARG EF 5 A_FaceTarget
        SARG G 5 A_CustomMeleeAttack(random(15,20),"bauk/attack","bauk/attack")
        Goto See
    Pain:
        SARG H 2
        SARG H 2 A_Pain
        Goto See
    Death:
        SARG I 8
        SARG J 8 A_Scream
        SARG K 4
        SARG L 4 A_NoBlocking
        SARG M 4
        SARG N -1
        Stop
  }
}
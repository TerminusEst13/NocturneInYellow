ACTOR Vodyanoy 31094
{
//$Category Nocturne-Monsters
    Health 600
    Radius 48
    Height 64
    Speed 5
    PainChance 96
    PainChance "Spear", 255
    Scale 1.25
    Mass 2000
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "vodyanoy/see"
    PainSound "vodyanoy/pain"
    DeathSound "vodyanoy/death"
    ActiveSound "vodyanoy/idle"
    Obituary " "
    States
    {
    Spawn:
        INCB A 10 A_Look
        Loop
    See:
        INCB AABB 5 A_Chase
        TNT1 A 0 Radius_Quake(1,2,0,12,0)
        TNT1 A 0 A_PlaySound("vodyanoy/walk","soundslot6",1.0,0,ATTN_NORM)
        INCB CCDD 5 A_Chase
        TNT1 A 0 Radius_Quake(1,2,0,12,0)
        TNT1 A 0 A_PlaySound("vodyanoy/walk","soundslot6",1.0,0,ATTN_NORM)
        Loop
    Missile:
        INCB E 8
        TNT1 A 0 A_PlaySoundEx("vodyanoy/attack","soundslot5",0,0)
        INCB H 8 Bright A_CustomMissile("VodyanoyShot")
        INCB E 1 A_CPosRefire
        Goto Missile
    Pain:
        INCB J 3
        INCB J 3 A_Pain
        Goto See
    Death:
        INCB I 6 A_Scream
        INCB J 6
        INCB K 6 A_NoBlocking
        INCB LMN 6
        INCB N -1 A_BossDeath
        Stop
    }
}

actor VodyanoyShot
{
    Radius 11
    Height 8
    Speed 11
    Damage (random(20,35))
    Scale 1.1
    RenderStyle Add
    Alpha 0.99
    Projectile
    +SEEKERMISSILE
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    DeathSound "vodyanoy/impact"
    States
    {
    Spawn:
        TNT1 A 0 A_JumpIfInventory("Counter",20,"Vanish")
        // Not using A_Countdown/ReactionTime since I want them to vanish, not go to Death.
        FATB A 3 Bright A_GiveInventory("Counter",1)
        FATB B 3 Bright A_SeekerMissile(45,100,SMF_PRECISE | SMF_CURSPEED)
        Loop
    Death:
        FBXP A 8 Bright
        FBXP B 6 Bright
        FBXP C 4 Bright
        Stop
    Vanish:
        FATB AAAAAAAAAA 3 Bright A_FadeOut(0.1)
        stop
    }
}
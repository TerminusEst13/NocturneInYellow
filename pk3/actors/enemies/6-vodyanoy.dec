ACTOR Vodyanoy 31094
{
//$Category Nocturne-Monsters
    Health 600
    Radius 48
    Height 64
    Speed 5
    PainChance 96
    PainChance "Spear", 255
    DamageFactor "Bullet", 3.0
    Scale 1.25
    Mass 2000
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTBLAST
    +NOINFIGHTING
    +THRUSPECIES
    Species "Monster"
    SeeSound "vodyanoy/see"
    PainSound "vodyanoy/pain"
    DeathSound "vodyanoy/death"
    ActiveSound "vodyanoy/idle"
    Obituary " "
    States
    {
    Spawn:
        VODY A 10 A_Look
        Loop
    See:
        VODY AABB 5 A_Chase
        TNT1 A 0 Radius_Quake(1,2,0,12,0)
        TNT1 A 0 A_PlaySound("vodyanoy/walk","soundslot6",1.0,0,ATTN_NORM)
        VODY CCDD 5 A_Chase
        TNT1 A 0 Radius_Quake(1,2,0,12,0)
        TNT1 A 0 A_PlaySound("vodyanoy/walk","soundslot6",1.0,0,ATTN_NORM)
        Loop
    Missile:
        VODY A 8
        TNT1 A 0 A_PlaySoundEx("vodyanoy/attack","soundslot5",0,0)
        VODY B 8 Bright A_CustomMissile("VodyanoyShot")
        VODY A 1 A_CPosRefire
        Goto Missile
    Pain:
        VODY A 3
        VODY A 3 A_Pain
        Goto See
    Death:
        VODY E 4 A_Scream
        VODY FG 4
        VODY H 4 A_NoBlocking
        VODY IJKLMN 4
        VODY O -1 A_BossDeath
        Stop
    }
}

actor VodyanoyShot
{
    Radius 11
    Height 8
    Speed 11
    Damage (random(10,20))
    Scale 1.1
    RenderStyle Add
    Alpha 0.99
    Projectile
    +SEEKERMISSILE
    +RANDOMIZE
    +THRUSPECIES
    Species "Monster"
    DeathSound "vodyanoy/impact"
    States
    {
    Spawn:
        TNT1 A 0 A_JumpIfInventory("Counter",20,"Vanish")
        // Not using A_Countdown/ReactionTime since I want them to vanish, not go to Death.
        TNT1 A 0 A_GiveInventory("Counter",1)
        VODB AAA 1 Bright A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        TNT1 A 0 A_SeekerMissile(45,100,SMF_PRECISE | SMF_CURSPEED)
        VODB BBB 1 Bright A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        TNT1 AAAAAAA 0 A_SpawnItemEx("VodyanoyShotSpatter",0,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,160)
        VODB C 6 Bright
        VODB D 4 Bright
        VODB EFG 2 Bright
        Stop
    Vanish:
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        TNT1 A 0 A_SpawnItemEx("VodyanoyShotTrail",0,0,0,0,0,0,0,160)
        VODB A 1 Bright A_FadeOut(0.1)
        stop
    }
}

actor VodyanoyShotTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.75
    Scale 0.85
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1
        TNT1 AA 0 A_SpawnItemEx("VodyanoyShotTrail2",frandom(-0.25,0.25),random(-0.25,0.25),random(-0.25,0.25),random(-1,1),random(-1,1),random(-1,1),0,160)
        VODB AAAA 1 BRIGHT A_FadeOut(0.05)
        VODB BBBB 1 BRIGHT A_FadeOut(0.1)
        stop
    }
}

actor VodyanoyShotTrail2 : VodyanoyShotTrail
{
    Scale 0.775
    States
    {
    Spawn:
        TNT1 A 2
        VODB AAAA 1 BRIGHT A_FadeOut(0.05)
        VODB BBBB 1 BRIGHT A_FadeOut(0.1)
        stop
    }
}

actor VodyanoyShotSpatter
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Alpha 0.75
    Scale 0.75
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        VODB C 6 Bright
        VODB D 4 Bright
        VODB EFG 2 Bright
        Stop
    }
}
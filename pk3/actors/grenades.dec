ACTOR IronGrenade
{
    Radius 12
    Height 8
    Speed 35
    Damage (40)
    Scale 0.5
    Projectile
    BounceType Doom
    -NOGRAVITY
    -EXPLODEONWATER
    +CANBOUNCEWATER
    ReactionTime 47 // Just over four seconds
    BounceFactor 0.6
    BounceCount 6
    BounceSound "grenade/bounce"
    WallBounceSound "grenade/bounce"
    States
    {
    Spawn:
        TNT1 A 0 A_Countdown
        DIRN AAA 1 A_CustomMissile("NadeTrail",frandom(1,6),frandom(-4,4),0,0,0)
        TNT1 A 0 A_Countdown
        DIRN BBB 1 A_CustomMissile("NadeTrail",frandom(1,6),frandom(-4,4),0,0,0)
        TNT1 A 0 A_Countdown
        DIRN CCC 1 A_CustomMissile("NadeTrail",frandom(1,6),frandom(-4,4),0,0,0)
        TNT1 A 0 A_Countdown
        DIRN DDD 1 A_CustomMissile("NadeTrail",frandom(1,6),frandom(-4,4),0,0,0)
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("grenade/boom","soundslot5",0,0)
        TNT1 A 0 A_PlaySoundEx("grenade/boomfar","soundslot6",0,1)
        TNT1 A 0 A_Explode(64,160,0) //This is the part of the explosion that doesn't hurt the shooter.
        TNT1 A 0 A_Explode(32,128,1) //This is the part of the explosion that does hurt the shooter.
        TNT1 A 0 A_SpawnItemEx("GrenadeExplosionRing",0,0,4,0,0,0,0,128,0)
        TNT1 AAAAA 0 A_SpawnItemEx("GrenadeExplosion",frandom(-4,4),frandom(-4,4),frandom(6,14),frandom(-1,1),frandom(-1,1),frandom(-1,1),0,128,0)
        TNT1 A 0 Radius_Quake(4,16,0,8,0) // Close range? Lots of shaking!
        TNT1 A 0 Radius_Quake(2,12,0,28,0) // Kind of far? Not a lot of shaking!
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GrenadeShrapnel",frandom(-1,1),frandom(-1,1),frandom(2,8),frandom(-7,7),frandom(-7,7),frandom(4,15),random(-180,180),32) // 10
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GrenadeShrapnel",frandom(-1,1),frandom(-1,1),frandom(2,8),frandom(-7,7),frandom(-7,7),frandom(4,15),random(-180,180),32) // 20
        TNT1 AAAAA 0 A_SpawnItemEx("GrenadeShrapnel",frandom(-1,1),frandom(-1,1),frandom(2,8),frandom(-7,7),frandom(-7,7),frandom(4,15),random(-180,180),32) // 30
        TNT1 AAAA 0 A_SpawnItemEx("ExplosionSmoke",frandom(-3,3),frandom(-3,3),frandom(-3,3),frandom(0.5,1.25),frandom(0.5,1.25),random(0.5,1.25),random(0,360),128,0)
        TNT1 AAAAAAA 5 A_SpawnItemEx("ExplosionSmoke",frandom(-3,3),frandom(-3,3),frandom(-3,3),frandom(1,1.75),frandom(1,1.75),frandom(1,2),random(-180,180),128,0)
        Stop
    }
}

ACTOR GrenadeExplosionRing
{
    +NOINTERACTION
    +DONTSPLASH
    +CLIENTSIDEONLY
    Renderstyle Add
    Scale 2.25
    States
    {
    Spawn:
        TNT1 A 0
        EXPL ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        stop
    }
}

ACTOR GrenadeExplosion
{
    +NOINTERACTION
    +DONTSPLASH
    +CLIENTSIDEONLY
    Renderstyle Add
    Scale 2.0
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_Jump(256,"Exp1","Exp3","Exp4")
        TNT1 A 1
        loop
    Exp1:
        EXP1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        Stop
    Exp2:
        EXP2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        Stop
    Exp3:
        EXP3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        Stop
    Exp4:
        EXP4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        Stop
    Exp5:
        EXP5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
        Stop
    }
}

ACTOR NadeTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +DONTSPLASH
    Renderstyle Add
    Scale 0.3
    Alpha 0.035
    States
    {
    Spawn:
        TNT1 A 1
        ISMK ABCDEFGHIJKLMOPQRSTUVWXYZ 1
        Stop
    }
}

ACTOR GrenadeShrapnel
{
    PROJECTILE
    +WINDTHRUST
    +MOVEWITHSECTOR
    +SLIDESONWALLS
    +PUSHABLE
    +DOOMBOUNCE
    -NOGRAVITY
    -SOLID
    //+NOINTERACTION
    +CLIENTSIDEONLY
    Radius 1
    Height 1
    Scale 0.7
    BounceFactor 0.6
    BounceCount 8
    States
    {   
    Spawn:
        ISHD A 4
        ISHD B 4
        ISHD C 4
        Loop 
    Death:
        ISHD A 0 A_Jump(255,"ShardA","ShardB","ShardC","ShardD","ShardE","ShardF","ShardG","ShardH")
        ISHD A 1
        loop
    ShardA:
        ISHD A -1
        Stop
    ShardB:
        ISHD B -1
        Stop
    ShardC:
        ISHD C -1
        Stop
    ShardD:
        ISHD D -1
        Stop
    ShardE:
        ISHD E -1
        Stop
    ShardF:
        ISHD F -1
        Stop
    ShardG:
        ISHD G -1
        Stop
    ShardH:
        ISHD H -1
        Stop
    }
}

ACTOR ExplosionSmoke
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Scale 0.35
    Alpha 0.7
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,"SmokeA","SmokeB","SmokeC","SmokeD","SmokeE","SmokeF","SmokeG","SmokeH","SmokeI","SmokeJ","SmokeK","SmokeL","SmokeN","SmokeM","SmokeO")
        TNT1 A 1
        loop
    SmokeA:
        SMOK A 2 A_FadeOut(0.025)
    SmokeB:
        SMOK B 2 A_FadeOut(0.025)
    SmokeC:
        SMOK C 2 A_FadeOut(0.025)
    SmokeD:
        SMOK D 2 A_FadeOut(0.025)
    SmokeE:
        SMOK E 2 A_FadeOut(0.025)
    SmokeF:
        SMOK F 2 A_FadeOut(0.025)
    SmokeG:
        SMOK G 2 A_FadeOut(0.025)
    SmokeH:
        SMOK H 2 A_FadeOut(0.025)
    SmokeI:
        SMOK I 2 A_FadeOut(0.025)
    SmokeJ:
        SMOK J 2 A_FadeOut(0.025)
    SmokeK:
        SMOK K 2 A_FadeOut(0.025)
    SmokeL:
        SMOK L 2 A_FadeOut(0.025)
    SmokeM:
        SMOK M 2 A_FadeOut(0.025)
    SmokeN:
        SMOK N 2 A_FadeOut(0.025)
    SmokeO:
        SMOK O 2 A_FadeOut(0.025)
        goto SmokeA
    }
}
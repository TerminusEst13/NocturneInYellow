ACTOR "Koroleva" : Weapon // Means "Queen"
{
    Weapon.SelectionOrder 90
    Weapon.SlotNumber 1
    Weapon.BobStyle Alpha
    Weapon.BobRangeX 0.9
    Weapon.BobRangeY 0.5
    Weapon.BobSpeed 1.25
    Weapon.AmmoType2 "GrenadeAmmo"
    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 0
    Decal "BulletChip"
    Obituary "%k tore through %o."
    +WEAPON.NOAUTOFIRE
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
    +WEAPON.ALT_AMMO_OPTIONAL
    States
    {
    Ready:
        TNT1 A 0 A_GiveInventory("WeaponIdle",1)
        //TNT1 A 0 A_JumpIfInventory("SprintDash",1,"D_RUNNIN")
        ROGA A 1 A_WeaponReady
        Loop
    Deselect:
        TNT1 AA 0 A_Lower
        ROGA A 1 A_Lower
        Loop
    Select:
        TNT1 AA 0 A_Raise
        ROGA A 1 A_Raise
        Loop
    Fire:
        TNT1 A 0 A_TakeInventory("WeaponIdle",1)
        ROGA BCD 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 3
    Hold:
        TNT1 A 1
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_PlayWeaponSound("rogatina/swing")
        ROGA EF 1
        TNT1 A 0 A_FireCustomMissile("rogatinaStab",0,0,0,0)
        TNT1 A 0 A_Recoil(-0.4)
        ROGA G 1 A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
        ROGA H 1 Offset(0,24) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(-1,20) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(-2,19) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(-3,20) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(-2,23) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(0,29) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
		ROGA H 1 Offset(0,33) A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
        ROGA IJKLM 1 A_JumpIfInventory("rogatinaWallHit",1,"WallHit")
        TNT1 A 0 A_TakeInventory("rogatinaWallHit",1) // Just in case
        goto Ready
    WallHit:
        TNT1 A 0 A_Recoil(frandom(1.0,2.0))
        TNT1 A 0 A_SetAngle(angle+frandom(-1.5,1.5))
        TNT1 A 0 A_SetPitch(pitch-frandom(1.0,2.0))
        ROGA N 1 Offset(0,40)
		ROGA P 1 Offset(12,36)
        ROGA Q 1 Offset(2,32)
		ROGA Q 1 Offset(18,30)
        ROGA R 1 Offset(67,25)
        ROGA R 1 Offset(70,27)
        ROGA R 1 Offset(35,30)
        ROGA R 1 Offset(10,32)
        ROGA S 1 Offset(0,32)
        ROGA S 1 Offset(-7,36)
        ROGA TTUV 1 A_TakeInventory("rogatinaWallHit",1)
        goto Ready
    Spawn:
        SHOT A -1
        Stop
        
    D_RUNNIN:
        ROGA A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 1
    IdleLoop:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("SprintDash",1,"IdleLoop")
        goto BackToNormal
    BackToNormal:
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto Ready

    AltFire:
        TNT1 A 0 A_TakeInventory("WeaponIdle",1)
        TNT1 A 0 A_TakeInventory("WeaponIdleCounter",0x7FFFFFFF)
        ROGA B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        ROGA D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 2
    Throw:
        TNT1 A 0 A_JumpIfNoAmmo("ThrowBlank")
        TNT1 A 6
        THRW ABC 1
        TNT1 A 0 A_PlayWeaponSound("grenade/throw")
        THRW DE 1
        THRW F 1 A_FireCustomMissile("IronGrenade",0,1,2,-4,0,10)
        THRW GHI 1
        TNT1 A 2
        Goto BackToNormal
    ThrowBlank:
        TNT1 A 6
        THRW A 1
        TNT1 A 0 A_PlayWeaponSound("grenade/throw")
        THRW BC 1
        THRW DE 1
        THRW F 1
        THRW GHI 1
        TNT1 A 2
        Goto BackToNormal
    }
}

actor RogatinaStab : FastProjectile
{
    Radius 2
    Height 2
    Damage (100)
    Speed 128
    RenderStyle Add
    Alpha 0.6
    Scale 0.65
    Decal ArrowCrack
    DamageType "Spear"
    ProjectileKickBack 150
    Projectile
    +BLOODSPLATTER
    +SKYEXPLODE
    +EXPLODEONWATER
    +NOEXTREMEDEATH
    States
    {
    Spawn:
        TNT1 A 1
        stop
    Death:
        TNT1 A 1
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("RogatinaWallHit",1)
        TNT1 A 0 A_SpawnItemEx("RogatinaQuake",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_PlaySound("rogatina/wall",CHAN_AUTO,1.0,0,ATTN_NORM)
        TNT1 AAAAA 0 A_SpawnItemEx("ArrowSmoke",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(0.1,0.5),random(-180,180),32)
        TNT1 A 1
        stop
    XDeath:
    Crash:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("RogatinaQuake2",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_PlaySound("rogatina/hit",CHAN_AUTO,1.0,0,ATTN_NORM)
        TNT1 A 1
        stop
    }
}

actor RogatinaQuake
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SOLID
    +NOCLIP
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 Radius_Quake(2,6,0,16,0)
        TNT1 A 1
        stop
    }
}

actor RogatinaQuake2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    -SOLID
    +NOCLIP
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 Radius_Quake(5,10,0,16,0)
        TNT1 A 1
        stop
    }
}